--
-- Minila interpreter
--

mod* VAR principal-sort Var {
  [Var]
}

mod! EXP { 
  pr(VAR) 
  pr(NAT)
  [Var < Exp]
  op n : Nat -> Exp {constr} .
  op _+_ : Exp Exp -> Exp {constr prec: 33 l-assoc} .
  op _-_ : Exp Exp -> Exp {constr prec: 33 l-assoc} .
  op _*_ : Exp Exp -> Exp {constr prec: 31 l-assoc} .
  op _/_ : Exp Exp -> Exp {constr prec: 31 l-assoc} .
  op _%_ : Exp Exp -> Exp {constr prec: 31 l-assoc} .
  op _===_ : Exp Exp -> Exp {constr prec: 40 l-assoc} .
  op _=!=_ : Exp Exp -> Exp {constr prec: 40 l-assoc} .
  op _<_ : Exp Exp -> Exp {constr prec: 40 l-assoc} .
  op _>_ : Exp Exp -> Exp {constr prec: 40 l-assoc} .
  op _&&_ : Exp Exp -> Exp {constr prec: 50 l-assoc} .
  op _||_ : Exp Exp -> Exp {constr prec: 55 l-assoc} .
}

open EXP .
  ops x y z : -> Var .
  -- expressionの解析
  red x + y - z * n(2) / x === n(10) + x * y || x =!= y
  && n(1) + x + y > z .
close

mod! STM {
  pr(EXP)
  [Stm]
  op estm : -> Stm {constr} .
  op _:=_; : Var Exp -> Stm {constr} .
  op if_{_}else{_} : Exp Stm Stm -> Stm {constr} .
  op while_{_} : Exp Stm -> Stm {constr} .
  op for_ _ _{_} : Var Exp Exp Stm -> Stm {constr} .
  op _ _ : Stm Stm -> Stm {constr prec: 60 id: estm l-assoc} .
}

--
-- Some modules (data stuructures) used in the Minila
-- implementation.
--

mod! BOOL-ERR {
  pr(BOOL)
  [Bool ErrBool < Bool&Err]
  op errBool : -> ErrBool {constr} .
}

mod! NAT-ERR {
  pr(NAT)
  pr(BOOL-ERR)
  [Nat ErrNat < Nat&Err]
  op errNat : -> ErrNat {constr} .
  op p_ : Nat&Err -> Nat&Err .
  op _+_ : Nat&Err Nat&Err -> Nat&Err {assoc comm} .
  op _*_ : Nat&Err Nat&Err -> Nat&Err {assoc comm} .
  op sd : Nat&Err Nat&Err -> Nat&Err .
  op _quo_ : Nat&Err Nat&Err -> Nat&Err .
  op _rem_ : Nat&Err Nat&Err -> Nat&Err .
  op _<_ : Nat&Err Nat&Err -> Bool&Err .
  op _>_ : Nat&Err Nat&Err -> Bool&Err .
  op if_then{_}else{_} : Bool&Err Nat&Err Nat&Err -> Nat&Err .
  --
  var N : Nat .
  vars N&E N&E1 N&E2 : Nat&Err .
  -- p_
  eq p 0 = errNat .
  eq p errNat = errNat .
  -- _+_
  eq errNat + N&E = errNat .
  -- _*_
  eq errNat * N&E = errNat .
  -- sd
  eq sd(errNat,N&E) = errNat .
  eq sd(N&E,errNat) = errNat .
  -- _quo_
  eq N quo 0 = errNat .
  eq errNat quo N&E = errNat .
  eq N&E quo errNat = errNat .
  -- _rem_
  eq N rem 0 = errNat .
  eq errNat rem N&E = errNat .
  eq N&E rem errNat = errNat .
  -- _<_
  eq errNat < N&E = errBool .
  eq N&E < errNat = errBool .
  -- _>_
  eq errNat > N&E = errBool .
  eq N&E > errNat = errBool .
  -- if_then{_}else{_}
  eq if true then {N&E1} else {N&E2} = N&E1 .
  eq if false then {N&E1} else {N&E2} = N&E2 .
}

mod! PAIR (M :: TRIV, N :: TRIV) {
  [Pair]
  op (_,_) : Elt.M Elt.N -> Pair {constr} .
}

mod* TRIV-ERR {
  [Elt Err < Elt&Err]
  op err : -> Err {constr} .
}

mod! LIST (M :: TRIV-ERR) {
  pr(NAT-ERR)
  [List]
  op nil : -> List {constr} .
  op _|_ : Elt.M List -> List {constr} .
  op _@_ : List List -> List {r-assoc} .
  op nth : List Nat&Err -> Elt&Err.M .
  op len : List -> Nat .
  vars X Y : Elt.M .
  vars L L1 : List .
  var N : Nat .
  var NzN : NzNat .
  -- _@_
  eq nil @ L = L .
  eq (X | L1) @ L = X | (L1 @ L) .
  -- nth
  eq nth(L,errNat) = err.M .
  eq nth(nil,N) = err.M .
  eq nth((X | L),0) = X .
  eq nth((X | L),NzN) = nth(L,p NzN) .
  -- len
  eq len(nil) = 0 .
  eq len(X | L) = len(L) + 1 .
}

--
-- Module VAR is declared in ast.cafe.
--
mod! ENTRY {
  -- varとnatでpairを具体化してentryと名付け
  pr(PAIR(VAR,NAT) * {sort Pair -> Entry})
  [Entry ErrEntry < Entry&Err]
  op errEntry : -> ErrEntry .
}

view TRIV-ERR2ENTRY from TRIV-ERR to ENTRY {
  sort Elt -> Entry,
  sort Err -> ErrEntry,
  sort Elt&Err -> Entry&Err,
  op err -> errEntry
}

--
-- Variables, together with values (natural numbers) are
-- registered into an environment.
-- An environment is implemented as tables from variables to
-- natural numbers (more concretely as lists of pairs of variables
-- and natural numbers).
--
mod! ENV {
  pr(NAT-ERR)
  -- pair(var,nat)のlist
  pr(LIST(M <= TRIV-ERR2ENTRY)
     * {sort List -> Env, op nil -> empEnv})
  [Env ErrEnv < Env&Err]
  op errEnv : -> ErrEnv {constr} .
  op _|_ : Entry&Err Env&Err -> Env&Err .
  op update : Env&Err Var Nat&Err -> Env&Err .
  op lookup : Env&Err Var -> Nat&Err .
  op if_then{_}else{_} : Bool&Err Env&Err Env&Err -> Env&Err .
  --
  var E : Env .
  vars V V1 : Var .
  vars N N1 : Nat .
  vars E&E E&E1 E&E2 : Env&Err .
  var N&E : Nat&Err .
  var ET&E : Entry&Err .
  -- _|_
  eq errEntry | E&E = errEnv .
  eq ET&E | errEnv = errEnv .
  -- update
  eq update(E&E,V,errNat) = errEnv .
  eq update(errEnv,V,N&E) = errEnv .
  eq update(empEnv,V,N) = (V,N) | empEnv .
  eq update((V1,N1) | E,V,N)
     = if V1 == V then {(V,N) | E} else {(V1,N1) | update(E,V,N)} .
  -- lookup
  eq lookup(errEnv,V) = errNat .
  eq lookup(empEnv,V) = errNat .
  eq lookup((V1,N1) | E,V)
     = if V1 == V then {N1} else {lookup(E,V)} .
  -- if_then{_}else{_}
  eq if true then {E&E1} else {E&E2} = E&E1 .
  eq if false then {E&E1} else {E&E2} = E&E2 .
}

open ENV .
  ops x y : -> Var .
  op env : -> Env .
  eq env = update(update(empEnv,x,0),y,1) .
  red env .
  red errEnv .
  red lookup(env,x) .
  red lookup(env,y) .
  red nth(env,1) .
  red nth(env,2) .
close

--
-- A stack is represented as a list of natural numbers.
--

view TRIV-ERR2NAT-ERR from TRIV-ERR to NAT-ERR {
  sort Elt -> Nat,
  sort Err -> ErrNat,
  sort Elt&Err -> Nat&Err,
  op err -> errNat
}

mod! STACK {
  pr(LIST(M <= TRIV-ERR2NAT-ERR)
     * {sort List -> Stack, op nil -> empstk} )
  [Stack ErrStack < Stack&Err]
  op errStack : -> ErrStack {constr} .
  op _|_ : Nat&Err Stack&Err -> Stack&Err .
  --
  var S&E : Stack&Err .
  var N&E : Nat&Err .
  -- _|_
  eq errNat | S&E = errStack .
  eq N&E | errStack = errStack .
}


--
-- - Function interpret interprets a given Minila program,
--   returning the environment made by executing the progam.
-- - Function eval takes a Minila program and an environment 
--   and executes the program under the environment, returning
--   the environment updated by executing the progam.
-- - Function evalAssign executes an assignment under a given
--   environment.
-- - Function evalIf executes an If statement under a given
--   environment.
-- - Function evalWhile executes a While statement under a given
--   environment.
-- - Function evalExp computes an expression under a given
--   environment, returning the result (a natural number).

mod! INTERPRET {
  pr(STM)
  pr(ENV)
  op interpret : Stm -> Env&Err .
  op eval : Stm Env&Err -> Env&Err .
  op evalAssign : Var Nat&Err Env&Err -> Env&Err .
  op evalIf : Nat&Err Stm Stm Env&Err -> Env&Err .
  op evalWhile : Nat&Err Exp Stm Env&Err -> Env&Err .
  op evalExp : Exp Env&Err -> Nat&Err .
  vars E E1 E2 : Exp .
  vars S S1 S2 : Stm .
  var V : Var .
  var EV : Env .
  var N : Nat .
  var NzN : NzNat .
  -- interpret
  eq interpret(S) = eval(S,empEnv).
  -- eval
  eq eval(S,errEnv) = errEnv .
  -- emptyStmの場合は処理系に変更を加えない
  eq eval(estm,EV) = EV .
  eq eval(V := E ;,EV) = evalAssign(V,evalExp(E,EV),EV) .
  eq eval(if E {S1} else {S2},EV) = evalIf(evalExp(E,EV),S1,S2,EV) .
  eq eval(while E {S1},EV) = evalWhile(evalExp(E,EV),E,S1,EV) .
  -- evalAssign
  -- 等式を3つ定義する
  -- 2つは2番目の引数がErrNatのときErrEnvを返す
  -- 3番目がErrEnvのときErrEnvを返す
  -- 2番目の引数が自然数、3番目の引数が環境のとき、環境(table)に自然数と変数を登録する update
  eq evalAssign(V,errNat,EV) = errEnv .
  eq evalAssign(V,N,errEnv) = errEnv .
  -- evalIf
  -- 3つか4つ
  -- 2つはエラー処理 1つめがerrNatのときerrEnv, 4つ目がerrEnvのときerrEnv
  -- 自然数を2つに分ける 0と0以外 NZNat Fact参照
  eq evalIf(errNat,S1,S2,EV) = errEnv .
  eq evalIf(N,S1,S2,errEnv) = errEnv .
  eq evalIf(0,S1,S2,EV) = eval(S2,EV) .
  eq evalIf(NzN,S1,S2,EV) = eval(S1,EV) .

  -- evalWhile
  -- 等式は4つ
  eq evalWhile(errNat,E,S1,EV) = errEnv .
  eq evalWhile(N,E,S1,errEnv) = errEnv .
  eq evalWhile(0,E,S1,EV) = EV .
  eq evalWhile(NzN,E,S1,EV) = eval(while E{S1}, eval(S1,EV)) .
  -- evalExp
  -- errEnvが含まれる場合はerrNatを返す
  eq evalExp(E,errEnv) = errNat .
  -- 自然数の評価 n(N)はN
  -- 左辺はminilaの世界の自然数の表現
  -- 右辺はcafeobjの世界の自然数の表現
  eq evalExp(n(N),EV) = N .
  -- 変数の評価
  -- 環境に対して変数の値を問い合わせる
  eq evalExp(V,EV) = lookup(EV,V) .
  -- 足し算の解釈実行
  eq evalExp(E1 + E2,EV) = evalExp(E1,EV) + evalExp(E2,EV) .
  -- 比較演算
  -- 片方がerrで片方がnatの場合errNatを返す(minilaで解釈できない)
  eq evalExp(E1 === E2,EV) = if evalExp(E1,EV) == errNat or evalExp(E2,EV) == errNat then {errNat} else {if evalExp(E1,EV) == evalExp(E2,EV) then {1} else {0}} .

  eq evalExp(E1 - E2,EV) = sd(evalExp(E1,EV),evalExp(E2,EV)) .
  eq evalExp(E1 && E2,EV) = if evalExp(E1,EV) == errNat or evalExp(E2,EV) == errNat then {errNat} else {if evalExp(E1,EV) == 0 or evalExp(E2,EV) == 0 then {0} else {1}} .
}

-- eof

open INTERPRET .
  ops x y z tmp : -> Var .
  
  op p0 : -> Stm .
  eq p0 = x := n(1) ;
          y := n(2) ;
          .
  red interpret(p0) .

  op p1 : -> Stm .
  -- 自然数は式 0ならn(0)
  -- :=+;で代入文
  eq p1 = x := n(1) ;
          y := n(1) ;
          while y < n(10) || y === n(10) {
            x := x * y ;
            y := y + n(1) ;
          } .
  red interpret(p1) .

  op p2 : -> Stm .
  eq p2 = x := n(1) ;
          for y n(1) n(10) {
            x := y * x ;
          } .
  red interpret(p2) .

  op p3 : -> Stm .
  eq p3 = x := n(24) ;
          y := n(30) ;
          while y =!= n(0) {
            z := x % y ;
            x := y ;
            y := z ;
          } .
  red interpret(p3) .

  op p4 : -> Stm .
  eq p4 = x := n(20000000000000000) ;
          y := n(0) ;
          z := x ;
          while y =!= z {
            if ((z - y) % n(2)) === n(0) {
              tmp := y + (z - y) / n(2) ;
            } else {
              tmp := y + ((z - y) / n(2)) + n(1) ;
            }
            if tmp * tmp > x {
              z := tmp - n(1) ;
            } else {
              y := tmp ;
            }
          } .
  red interpret(p4) .
close