--
-- Variables
--
mod* VAR principal-sort Var {
  [Var]
}

--
-- Expressions
--
mod! EXP {
  pr(VAR)
  pr(NAT)
  [Var < Exp]
  op n : Nat -> Exp {constr} .
  op _+_ : Exp Exp -> Exp {constr prec: 33 l-assoc} .
  op _-_ : Exp Exp -> Exp {constr prec: 33 l-assoc} .
  op _*_ : Exp Exp -> Exp {constr prec: 31 l-assoc} .
  op _/_ : Exp Exp -> Exp {constr prec: 31 l-assoc} .
  op _%_ : Exp Exp -> Exp {constr prec: 31 l-assoc} .
  op _===_ : Exp Exp -> Exp {constr prec: 40 l-assoc} .
  op _=!=_ : Exp Exp -> Exp {constr prec: 40 l-assoc} .
  op _<_ : Exp Exp -> Exp {constr prec: 40 l-assoc} .
  op _>_ : Exp Exp -> Exp {constr prec: 40 l-assoc} .
  op _&&_ : Exp Exp -> Exp {constr prec: 50 l-assoc} .
  op _||_ : Exp Exp -> Exp {constr prec: 55 l-assoc} .
}

--
-- Statements
--
mod! STM {
  pr(EXP)
  [Stm]
  op estm : -> Stm {constr} .
  op _:=_; : Var Exp -> Stm {constr} .
  op if_{_}else{_} : Exp Stm Stm -> Stm {constr} .
  op while_{_} : Exp Stm -> Stm {constr} .
  op for___{_} : Var Exp Exp Stm -> Stm {constr} .
  op __ : Stm Stm -> Stm {constr prec: 60 id: estm l-assoc} .
}

--
-- Some modules (data stuructures) used in the Minila
-- implementation.
--

mod! BOOL-ERR {
  pr(BOOL)
  [Bool ErrBool < Bool&Err]
  op errBool : -> ErrBool {constr} .
}

mod! NAT-ERR {
  pr(NAT)
  pr(BOOL-ERR)
  [Nat ErrNat < Nat&Err]
  op errNat : -> ErrNat {constr} .
  op p_ : Nat&Err -> Nat&Err .
  op _+_ : Nat&Err Nat&Err -> Nat&Err {assoc comm} .
  op _*_ : Nat&Err Nat&Err -> Nat&Err {assoc comm} .
  op sd : Nat&Err Nat&Err -> Nat&Err .
  op _quo_ : Nat&Err Nat&Err -> Nat&Err .
  op _rem_ : Nat&Err Nat&Err -> Nat&Err .
  op _<_ : Nat&Err Nat&Err -> Bool&Err .
  op _>_ : Nat&Err Nat&Err -> Bool&Err .
  op if_then{_}else{_} : Bool&Err Nat&Err Nat&Err -> Nat&Err .
  --
  var N : Nat .
  vars N&E N&E1 N&E2 : Nat&Err .
  -- p_
  eq p 0 = errNat .
  eq p errNat = errNat .
  -- _+_
  eq errNat + N&E = errNat .
  -- _*_
  eq errNat * N&E = errNat .
  -- sd
  eq sd(errNat,N&E) = errNat .
  eq sd(N&E,errNat) = errNat .
  -- _quo_
  eq N quo 0 = errNat .
  eq errNat quo N&E = errNat .
  eq N&E quo errNat = errNat .
  -- _rem_
  eq N rem 0 = errNat .
  eq errNat rem N&E = errNat .
  eq N&E rem errNat = errNat .
  -- _<_
  eq errNat < N&E = errBool .
  eq N&E < errNat = errBool .
  -- _>_
  eq errNat > N&E = errBool .
  eq N&E > errNat = errBool .
  -- if_then{_}else{_}
  eq if true then {N&E1} else {N&E2} = N&E1 .
  eq if false then {N&E1} else {N&E2} = N&E2 .
}

mod! PAIR (M :: TRIV, N :: TRIV) {
  [Pair]
  op (_,_) : Elt.M Elt.N -> Pair {constr} .
}

mod* TRIV-ERR {
  [Elt Err < Elt&Err]
  op err : -> Err {constr} .
}

mod! LIST (M :: TRIV-ERR) {
  pr(NAT-ERR)
  [List]
  op nil : -> List {constr} .
  op _|_ : Elt.M List -> List {constr} .
  op _@_ : List List -> List {r-assoc} .
  op nth : List Nat&Err -> Elt&Err.M .
  op len : List -> Nat .
  vars X Y : Elt.M .
  vars L L1 : List .
  var N : Nat .
  var NzN : NzNat .
  -- _@_
  eq nil @ L = L .
  eq (X | L1) @ L = X | (L1 @ L) .
  -- nth
  eq nth(L,errNat) = err.M .
  eq nth(nil,N) = err.M .
  eq nth((X | L),0) = X .
  eq nth((X | L),NzN) = nth(L,p NzN) .
  -- len
  eq len(nil) = 0 .
  eq len(X | L) = len(L) + 1 .
}

--
-- Module VAR is declared in ast.cafe.
--
mod! ENTRY {
  pr(PAIR(VAR,NAT) * {sort Pair -> Entry})
  [Entry ErrEntry < Entry&Err]
  op errEntry : -> ErrEntry .
}

view TRIV-ERR2ENTRY from TRIV-ERR to ENTRY {
  sort Elt -> Entry,
  sort Err -> ErrEntry,
  sort Elt&Err -> Entry&Err,
  op err -> errEntry
}

--
-- Variables, together with values (natural numbers) are
-- registered into an environment.
-- An environment is implemented as tables from variables to
-- natural numbers (more concretely as lists of pairs of variables
-- and natural numbers).
--
mod! ENV {
  pr(NAT-ERR)
  pr(LIST(M <= TRIV-ERR2ENTRY)
     * {sort List -> Env, op nil -> empEnv})
  [Env ErrEnv < Env&Err]
  op errEnv : -> ErrEnv {constr} .
  op _|_ : Entry&Err Env&Err -> Env&Err .
  op update : Env&Err Var Nat&Err -> Env&Err .
  op lookup : Env&Err Var -> Nat&Err .
  op if_then{_}else{_} : Bool&Err Env&Err Env&Err -> Env&Err .
  --
  var E : Env .
  vars V V1 : Var .
  vars N N1 : Nat .
  vars E&E E&E1 E&E2 : Env&Err .
  var N&E : Nat&Err .
  var ET&E : Entry&Err .
  -- _|_
  eq errEntry | E&E = errEnv .
  eq ET&E | errEnv = errEnv .
  -- update
  eq update(E&E,V,errNat) = errEnv .
  eq update(errEnv,V,N&E) = errEnv .
  eq update(empEnv,V,N) = (V,N) | empEnv .
  eq update((V1,N1) | E,V,N)
     = if V1 == V then {(V,N) | E} else {(V1,N1) | update(E,V,N)} .
  -- lookup
  eq lookup(errEnv,V) = errNat .
  eq lookup(empEnv,V) = errNat .
  eq lookup((V1,N1) | E,V)
     = if V1 == V then {N1} else {lookup(E,V)} .
  -- if_then{_}else{_}
  eq if true then {E&E1} else {E&E2} = E&E1 .
  eq if false then {E&E1} else {E&E2} = E&E2 .
}

open ENV .
  ops x y : -> Var .
  op env : -> Env .
  eq env = update(update(empEnv,x,0),y,1) .
  red env .
  red errEnv .
  red lookup(env,x) .
  red lookup(env,y) .
  red nth(env,1) .
  red nth(env,2) .
close

--
-- A stack is represented as a list of natural numbers.
--

view TRIV-ERR2NAT-ERR from TRIV-ERR to NAT-ERR {
  sort Elt -> Nat,
  sort Err -> ErrNat,
  sort Elt&Err -> Nat&Err,
  op err -> errNat
}

mod! STACK {
  pr(LIST(M <= TRIV-ERR2NAT-ERR)
     * {sort List -> Stack, op nil -> empstk} )
  [Stack ErrStack < Stack&Err]
  op errStack : -> ErrStack {constr} .
  op _|_ : Nat&Err Stack&Err -> Stack&Err .
  --
  var S&E : Stack&Err .
  var N&E : Nat&Err .
  -- _|_
  eq errNat | S&E = errStack .
  eq N&E | errStack = errStack .
}

--
-- An instruction set
--
mod! INSTRUCT principal-sort Instruct {
  pr(NAT)
  pr(VAR)
  [Instruct ErrInstruct < Instruct&Err]
  op errInstruct : -> ErrInstruct {constr} .
  op push : Nat -> Instruct {constr} .
  op load : Var -> Instruct {constr} .
  op store : Var -> Instruct {constr} .
  op multiply : -> Instruct {constr} .
  op divide : -> Instruct {constr} .
  op mod : -> Instruct {constr} .
  op add : -> Instruct {constr} .
  op minus : -> Instruct {constr} .
  op lessThan : -> Instruct {constr} .
  op greaterThan : -> Instruct {constr} .
  op equal : -> Instruct {constr} .
  op notEqual : -> Instruct {constr} .
  op and : -> Instruct {constr} .
  op or : -> Instruct {constr} .
  op jump : Nat -> Instruct {constr} .
  op bjump : Nat -> Instruct {constr} .
  op jumpOnCond : Nat -> Instruct {constr} .
  op quit : -> Instruct {constr} .
}

view TRIV-ERR2INSTRUCT from TRIV-ERR to INSTRUCT {
  sort Elt -> Instruct,
  sort Err -> ErrInstruct,
  sort Elt&Err -> Instruct&Err,
  op err -> errInstruct
}

--
-- An instruction sequence is represented as a list of instructions.
--
mod! ILIST {
  pr(LIST(M <= TRIV-ERR2INSTRUCT)
     * {sort List -> IList, op nil -> iln} )
}

--
-- - Function vm takes an instruction sequence and returns the
--   environment made by executing the instruction sequence.
-- - Function exec takes an instruction sequence, a program pointe
--   pointing to an instruction in the sequence, a stack, and an
--   environment, and execute the instruction under the environment
--   with the stack.
-- - Function exec2 is an auxiliary function of exec.
--
mod! VM {
  pr(ILIST)
  pr(ENV)
  pr(STACK)
  op run : IList -> Env&Err .
  op exec : IList Nat Stack&Err Env&Err -> Env&Err .
  op exec2 : Instruct&Err IList Nat Stack&Err Env&Err -> Env&Err .
  var IL : IList .
  -- program counter
  var PC : Nat .
  -- stack
  var Stk : Stack .
  -- envinroment
  var Env : Env .
  vars N N1 N2 : Nat .
  var V : Var .
  var E&E : Env&Err .
  var S&E : Stack&Err .
  var I&E : Instruct&Err .
  -- run
  eq run(IL) = exec(IL,0,empstk,empEnv) .
  -- exec
  eq exec(IL,PC,errStack,E&E) = errEnv .
  eq exec(IL,PC,S&E,errEnv) = errEnv .
  -- nth -> listのn番目の要素を取り出す
  eq exec(IL,PC,Stk,Env) = exec2(nth(IL,PC),IL,PC,Stk,Env) .
  -- exec2
  eq exec2(push(N),IL,PC,Stk,Env) = exec(IL,PC + 1,N | Stk,Env) .

  eq exec2(add,IL,PC,empstk,Env) = errEnv .
  eq exec2(add,IL,PC,N1 | empstk,Env) = errEnv .
  eq exec2(add,IL,PC,N2 | N1 | Stk,Env) = exec(IL,PC + 1,N2 + N1 | Stk,Env) .

  eq exec2(minus,IL,PC,empstk,Env) = errEnv .
  eq exec2(minus,IL,PC,N1 | empstk,Env) = errEnv .
  eq exec2(minus,IL,PC,N2 | N1 | Stk,Env) = exec(IL,PC + 1,sd(N2,N1) | Stk,Env) .

  eq exec2(divide,IL,PC,empstk,Env) = errEnv .
  eq exec2(divide,IL,PC,N1 | empstk,Env) = errEnv .
  eq exec2(divide,IL,PC,N2 | N1 | Stk,Env) = exec(IL,PC + 1,N2 quo N1 | Stk,Env) .

  eq exec2(multiply,IL,PC,empstk,Env) = errEnv .
  eq exec2(multiply,IL,PC,N1 | empstk,Env) = errEnv .
  eq exec2(multiply,IL,PC,N1 | N2 | Stk,Env) = exec(IL,PC + 1,N2 * N1 | Stk,Env) .

  eq exec2(divide,IL,PC,empstk,Env) = errEnv .
  eq exec2(divide,IL,PC,N1 | empstk,Env) = errEnv .
  eq exec2(divide,IL,PC,N2 | N1 | Stk,Env) = exec(IL,PC + 1,N2 quo N1 | Stk,Env) .

  eq exec2(mod,IL,PC,empstk,Env) = errEnv .
  eq exec2(mod,IL,PC,N1 | empstk,Env) = errEnv .
  eq exec2(mod,IL,PC,N2 | N1 | Stk,Env) = exec(IL,PC + 1,N2 rem N1 | Stk,Env) .

  eq exec2(store(V),IL,PC,empstk,Env) = errEnv .
  eq exec2(store(V),IL,PC,N1 | Stk,Env) = exec(IL,PC + 1, Stk, update(Env,V,N1)) .

  eq exec2(load(V),IL,PC,Stk,Env) = exec(IL,PC + 1,lookup(Env,V) | Stk,Env) .

  eq exec2(greaterThan,IL,PC,empstk,Env) = errEnv .
  eq exec2(greaterThan,IL,PC,N1 | empstk,Env) = errEnv .
  eq exec2(greaterThan,IL,PC,N1 | N2 | Stk,Env) = if N1 > N2 then {exec(IL,PC + 1,1 | Stk,Env)} else {exec(IL,PC + 1,0 | Stk,Env)} .

  eq exec2(lessThan,IL,PC,empstk,Env) = errEnv .
  eq exec2(lessThan,IL,PC,N1 | empstk,Env) = errEnv .
  eq exec2(lessThan,IL,PC,N1 | N2 | Stk,Env) = if N1 < N2 then {exec(IL,PC + 1,0 | Stk,Env)} else {exec(IL,PC + 1,1 | Stk,Env)} .

  eq exec2(equal,IL,PC,empstk,Env) = errEnv .
  eq exec2(equal,IL,PC,N1 | empstk,Env) = errEnv .
  eq exec2(equal,IL,PC,N1 | N2 | Stk,Env) = if N1 == N2 then {exec(IL,PC + 1,1 | Stk,Env)} else {exec(IL,PC + 1,0 | Stk,Env)} .

  eq exec2(notEqual,IL,PC,empstk,Env) = errEnv .
  eq exec2(notEqual,IL,PC,N1 | empstk,Env) = errEnv .
  eq exec2(notEqual,IL,PC,N1 | N2 | Stk,Env) = if N1 == N2 then {exec(IL,PC + 1,0 | Stk,Env)} else {exec(IL,PC + 1,1 | Stk,Env)} .

  eq exec2(and,IL,PC,empstk,Env) = errEnv .
  eq exec2(and,IL,PC,N1 | empstk,Env) = errEnv .
  eq exec2(and,IL,PC,N1 | N2 | Stk,Env) = if (not N1 == 0) and (not N2 == 0) then {exec(IL,PC + 1,1 | Stk,Env)} else {exec(IL,PC + 1,0 | Stk,Env)} .

  eq exec2(or,IL,PC,empstk,Env) = errEnv .
  eq exec2(or,IL,PC,N1 | empstk,Env) = errEnv .
  eq exec2(or,IL,PC,N1 | N2 | Stk,Env) = if (not N1 == 0) or (not N2 == 0) then {exec(IL,PC + 1,1 | Stk,Env)} else {exec(IL,PC + 1,0 | Stk,Env)} .

  eq exec2(jump(N),IL,PC,Stk,Env) = exec(IL,PC + N,Stk,Env) .

  eq exec2(bjump(N),IL,PC,Stk,Env) = exec(IL,sd(PC,N),Stk,Env) .

  eq exec2(jumpOnCond(N),IL,PC,empstk,Env) = errEnv . 
  eq exec2(jumpOnCond(N),IL,PC,N1 | Stk,Env) = if N1 == 0 then {exec(IL,PC + 1,Stk,Env)} else {exec(IL,PC + N,Stk,Env)} .

  eq exec2(quit,IL,PC,Stk,Env) = Env .
  -- load
  -- envからxをkeyにnを取り出してstackに積みpcをincrementする

  -- store
  -- stackに値があることを仮定する
  -- envに(x,n)を登録してstackからnを取り除く,pcをincrementする
  -- update(env,x,n)

  -- add
  -- stackから2つの自然数を取り出し(stackに自然数が2つあることを仮定)
  -- n1+n2をstack
  -- pcをincrement

  -- minus -> sd
  -- multiply -> *
  -- divide -> quo
  -- mod -> rem
  -- ↑addと同様

  -- lessThan
  -- stackから値を2つ取り出す
  -- n1 < n2 -> 1をstack
  -- else -> 0をstack

}

open VM .
  ops x y z : -> Var .
  ops il1 il2 il3 il4 il5 il6 il7 il8 il9 il10 il11 : -> IList .
  -- eq il1 = push(1) | store(x) | push(2) | store (y) | load(y) | load(y) | multiply |
  --          store(y) | load(x) | push(2) | multiply | store(x) | push(16) | load(x) |
  --          equal | jumpOnCond(2) | bjump(12) | quit | iln .
  -- red run(il1) .
  -- eq il2 = push(1) | store(x) | push(2) | store (y) | load(y) | load(y) | multiply |
  --          store(y) | load(x) | push(2) | multiply | store(x) | push(64) | load(x) |
  --          equal | jumpOnCond(2) | bjump(12) | quit | iln .
  -- red run(il2) .
  -- eq il3 = push(5) | push(15) | add | store(x) | quit | iln .
  -- eq il4 = push(5) | push(15) | minus | store(x) | quit | iln .
  -- eq il5 = push(15) | push(5) | divide | store(x) | quit | iln .
  -- eq il6 = push(15) | push(5) | mod | store(x) | quit | iln .
  -- eq il7 = push(15) | push(5) | equal | store(x) | quit | iln .
  -- eq il8 = push(15) | push(5) | notEqual | store(x) | quit | iln .
  -- eq il9 = push(15) | push(5) | lessThan | store(x) | quit | iln .
  -- eq il10 = push(15) | push(5) | greaterThan | store(x) | quit | iln .
  eq il11 = push(1) | store(x) | push(1) | store(y) | load(y) | push(10) |
            lessThan | load(y) | push(10) | equal | or |
            jumpOnCond(2) | jump(10) | load(x) | load(y) | 
            multiply | store(x) | load(y) | push(1) | add |
            store(y) | bjump(17) | quit | iln .
  -- red run(il3) .
  -- red run(il4) .
  -- red run(il5) .
  -- red run(il6) .
  -- red run(il7) .
  -- red run(il8) .
  -- red run(il9) .
  -- red run(il10) .
  red run(il11) .
close