-- gcd
open NAT .
  op gcd : Nat Nat -> Nat .
  var X : Nat .
  var NzY : NzNat .
  eq gcd(X,0) = X .
  eq gcd(X,NzY) = gcd(NzY,X rem NzY) .
  red gcd(0,0) .
  red gcd(2,0) .
  red gcd(0,2) .
  red gcd(24,36) .
  red gcd(2015,31031) .
close

-- factorial
open NAT .
  op fact : Nat -> Nat .
  var NzX : NzNat .
  eq fact(0) = 1 .
  eq fact(NzX) = NzX * fact(p NzX) .
  red fact(0) .
  red fact(1) .
  red fact(10) .
  red fact(100) .
close

-- odd-even divide and conquer factorial
open NAT .
  op cond : Bool Nat Nat -> Nat .
  op g : Nat Nat -> Nat .
  op oedc-fact : Nat -> Nat .
  vars X Y : Nat .
  var NzX : NzNat .
  eq cond(true,X,Y) = X .
  eq cond(false,X,Y) = Y .
  eq g(X,Y) = cond(X > Y, g(X,2 * Y) * g(sd(X,Y),2 * Y), X) .
  eq oedc-fact(0) = 1 .
  eq oedc-fact(NzX) = g(NzX,1) .
  red oedc-fact(0) .
  red oedc-fact(10) .
  red oedc-fact(100) .
close

-- fibonacci
open NAT .
  op fib : Nat -> Nat .
  op sfib : Nat -> Nat .
  var NzX : NzNat .
  eq fib(0) = 0 .
  eq fib(NzX) = sfib(p NzX) .
  eq sfib(0) = 1 .
  eq sfib(NzX) = fib(NzX) + fib(p NzX) .
  red fib(0) .
  red fib(1) .
  red fib(10) .
  red fib(20) .
close

-- ackerman
open NAT .
  op ack : Nat Nat -> Nat .
  var Y : Nat .
  vars NzX NzY : NzNat .
  eq ack(0, Y) = Y + 1 .
  eq ack(NzX, 0) = ack(p NzX,1) .
  eq ack(NzX,NzY) = ack(p NzX,ack(NzX,p NzY)) .
  red ack(0,0) .
  red ack(1,1) .
  red ack(2,2) .
close

-- 4 computing the summation v1
open NAT .
  op sum : Nat -> Nat .
  var X : Nat .
  eq sum(0) = 0 .
  eq sum(X) = X + sum(p X) .
  red sum(10) .
  red sum(100) .
close

-- 4. computing the summation v2
open NAT .
  op cond : Bool Nat Nat -> Nat .
  op sum : Nat Nat -> Nat .
  op oedc-sum : Nat -> Nat .
  var X : Nat .
  var Y : Nat .
  eq cond(true,X,Y) = X .
  eq cond(false,X,Y) = Y .
  eq sum(X,Y) = cond(X > Y,sum(X,2 * Y) + sum(sd(X,Y),2 * Y), X) .
  eq oedc-sum(0) = 0 .
  eq oedc-sum(1) = 1 .
  eq oedc-sum(X) = sum(X,1) .
  red oedc-sum(10) .
  red oedc-sum(100) .
close

-- 5. ext-fib
open NAT .
  op ext-fib : Nat -> Nat .
  op sfib : Nat -> Nat .
  op tfib : Nat -> Nat .
  vars X : NzNat .
  eq ext-fib(0) = 0 .
  eq ext-fib(X) = sfib(p X) .
  eq sfib(0) = 1 .
  eq sfib(X) = tfib(p X) .
  eq tfib(0) = 2 .
  eq tfib(X) = ext-fib(X + 1) + ext-fib(X) + ext-fib(X - 1) .
  red ext-fib(0) .
  red ext-fib(1) .
  red ext-fib(2) .
  red ext-fib(3) .
  red ext-fib(4) .
  red ext-fib(5) .
  red ext-fib(10) .
  red ext-fib(20) .
close

-- 6. factorial ! bang operator
open NAT .
  op _! : Nat -> Nat .
  var NzX : NzNat .
  eq 0 ! = 1 .
  eq NzX ! = NzX * (p NzX !) .
  red 0 ! .
  red 1 ! .
  red 10 ! .
  red 100 ! .
close

-- 7. odd-even divide and conquer factorial
open NAT .
  op if_then {_} else {_} : Bool Nat Nat -> Nat .
  op g : Nat Nat -> Nat .
  op _! : Nat -> Nat .
  vars X Y : Nat .
  var NzX : NzNat .
  eq if true then {X} else {Y} = X .
  eq if false then {X} else {Y} = Y .
  eq g(X,Y) = if (X > Y) then {g(X,2 * Y) * g(sd(X,Y),2 * Y)} else {X} .
  eq 0 ! = 1 .
  eq NzX ! = g(NzX,1) .
  red 0 ! .
  red 1 ! .
  red 10 ! .
  red 100 ! .
close

